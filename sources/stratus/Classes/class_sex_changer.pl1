/*
 *   class_sex_changer.pl1
 *
 *   Behavior module for object class sex_changer.
 *
 *   Chip Morningstar
 *   Lucasfilm Ltd.
 *   27-July-1986
 */

%include 'microcosm.incl.pl1';
%include 'defs_helper.incl.pl1';
%include 'defs_action.incl.pl1';

initialize_class_sex_changer: procedure;

     %replace SEX_CHANGER_REQUESTS by 4;

     declare a(0:SEX_CHANGER_REQUESTS) entry based;
     declare class_sex_changer_actions pointer;
     declare 1 sex_changer based %include struct_sex_changer;

     %replace I by CLASS_SEX_CHANGER;

     Class_Table(I).capacity = 0;
     Class_Table(I).max_requests = SEX_CHANGER_REQUESTS;
     Class_Table(I).alloc_size = size(sex_changer);
     Class_Table(I).pc_state_bytes = 0;
     Class_Table(I).known = true;
     Class_Table(I).opaque_container = false;
     Class_Table(I).filler = false;

     allocate a set(class_sex_changer_actions);
     Class_Table(I).actions = class_sex_changer_actions;

     Class_Table(I).actions->a(HELP) = generic_HELP; /* 0 */
     Class_Table(I).actions->a(1)     = illegal;           /* 1 */
     Class_Table(I).actions->a(2)     = illegal;           /* 2 */
     Class_Table(I).actions->a(3)     = illegal;           /* 3 */
     Class_Table(I).actions->a(SEXCHANGE) = sex_changer_SEXCHANGE; /* 4 */

end initialize_class_sex_changer;

sex_changer_SEXCHANGE: procedure;
     if (adjacent(selfptr)) then do;
          if (test_bit(avatar.orientation, 8)) then
               call clear_bit(avatar.orientation, 8);
          else
               call set_bit(avatar.orientation, 8);
          call n_msg_1(selfptr, SEXCHANGE$, avatar.noid);
     end;
     call r_msg_1(TRUE);
end sex_changer_SEXCHANGE;

;
;	generic_wearLegs.m
;
;	Action code for generic get-or-wear pants operation.
;
;	This file should be assembled as position independent code.
;
;	Chip Morningstar
;	Lucasfilm Ltd.
;	13-May-1986
;
	include	"action_head.i"

; This is the parameter returned by the GET request
define GET_SUCCESS = 0
; These are parameters for customization pattern swap call
define SWAP_OBJECT_OFFSET = 0x10
define SWAP_AVATAR_OFFSET = 0x11
define SWAP_MASK = 0x12

	actionStart

	lda in_hand_noid
	if (zero) {				; If hand is empty go get it
		containerAbort
		doMyAction ACTION_GO		; My GO walks to this object
		waitWhile animation_wait_bit	; Wait till we get there
		chore AV_ACT_bend_over
		sendMsg pointed_noid, MSG_GET, 0

		getResponse GET_SUCCESS
		if (!zero) {			; Non-zero is success
		        lda pointed_noid	; Put in hand
    			jsr v_get_subject_object
		        changeContainers 0, AVATAR_HAND, actor_noid
			lda #1
			newImage pointed_noid
			lda #AV_ACT_bend_back
			chainTo v_set_actor_chore
;        		rts			; Avoid beep below
		}
		lda #AV_ACT_bend_back
		jsr v_set_actor_chore
		chainTo v_beep			; Complain on failure
	}
	getProp actor, OBJECT_contents + AVATAR_LEGS
	if (zero) {		; Can't already be wearing something there
		cmp pointed_noid		; If holding garment
		if (equal) {			; Then put it on

			swapPatterns BOTTOM_legPattern, PATTERN_LEGS,
				LEGS_MASK, CLOTHING_PATTERN_SWAP

			; Move garment onto body
			jsr v_get_subject_object
			changeContainers 0, AVATAR_LEGS, actor_noid
			rts
		}
	}
	chainTo	v_beep				; If we get here we failed
	actionEnd


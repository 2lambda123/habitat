/*
 *   class_bag.pl1
 *
 *   Bag object behavior module for Habitat.
 *
 *   Chip Morningstar
 *   Lucasfilm Ltd.
 *   9-April-1986
 */

%replace BAG_CAPACITY by 5;

%include 'microcosm.incl.pl1';
%include 'defs_helper.incl.pl1';
%include 'defs_action.incl.pl1';

initialize_class_bag: procedure;

     %replace BAG_REQUESTS by 5;

     declare a(0:BAG_REQUESTS) entry based;
     declare class_bag_actions pointer;
     declare 1 bag based %include struct_bag;

     %replace I by CLASS_BAG;

     Class_Table(I).capacity = BAG_CAPACITY;
     Class_Table(I).max_requests = BAG_REQUESTS;
     Class_Table(I).alloc_size = size(bag);
     Class_Table(I).pc_state_bytes = 3;
     Class_Table(I).known = true;
     Class_Table(I).opaque_container = true;
     Class_Table(I).filler = false;

     allocate a set(class_bag_actions);
     Class_Table(I).actions = class_bag_actions;

     Class_Table(I).actions->a(HELP) = bag_HELP; /* 0 */
     Class_Table(I).actions->a(GET)            = generic_GET;           /* 1 */
     Class_Table(I).actions->a(PUT)            = generic_PUT;           /* 2 */
     Class_Table(I).actions->a(THROW)          = generic_THROW;         /* 3 */
     Class_Table(I).actions->a(CLOSECONTAINER) = generic_CLOSECONTAINER;/* 4 */
     Class_Table(I).actions->a(OPENCONTAINER)  = generic_OPENCONTAINER; /* 5 */

end initialize_class_bag;

bag_HELP: procedure;
     declare 1 self based(selfptr) %include struct_bag;

     call lock_HELP('Bag', self.key_hi * 256 + self.key_lo, self.open_flags);
end bag_HELP;

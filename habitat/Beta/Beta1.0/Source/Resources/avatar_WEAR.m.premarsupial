;
;	avatar_WEAR.m
;
;	Action code for the asynchronous wear an item of clothing operation.
;
;	This file should be assembled as position independent code.
;
;	Chip Morningstar
;	Lucasfilm Ltd.
;	22-May-1986
;
	include	"action_head.i"
	include "class_equates.m"

	actionStart

	lda in_hand_noid
	jsr v_get_subject_object
	sound	CLOTHES_DONNED, subject_noid
	getProp in_hand, OBJECT_class_pointer
	cmp #CLASS_PANTS
	if (equal) {
		ldy #AVATAR_LEGS
	} else {
		cmp #CLASS_HEAD
		if (equal) {
			ldy #AVATAR_HEAD
		} else {
			ldy #AVATAR_TORSO
		}
	}
	ldx #0
	lda actor_noid
	jsr v_change_containers
	lda	#0
	putProp	actor, AVATAR_background_actitviy
	rts

	actionEnd

